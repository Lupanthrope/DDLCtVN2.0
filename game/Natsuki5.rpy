label Natsuki5:
#Start Scene 9 - Cocoa
    
    if (karma >= 3):
        call Natsuki51
    else:
        call Natsuki52

    scene bg house
    with dissolve_scene_full
    "As we head out the door, I notice Sayori's holiday lights aren't on."
    mc "Hm. How strange..."
    show natsuki 1bc at t11 zorder 2 
    n "What's strange?"
    mc "Sayori's holiday lights aren't on. They usually are at this time of year, regardless if she's home or not."
    n 2bk "Maybe she forgot this year."
    n "She's been pretty off lately."
    n "I wouldn't be surprised if she forgot."
    mc "I'll remind her later."
    n 4bl "Hey, [player]."
    n "People at school were talking about the Christmas lights at the nearby mall."
    mc "Yeah, I overheard something about it."
    n 4bj "Do you think we could go see them? I never have before."
    mc "Sure, why not."
    "I don't have any other ideas, anyways."
    scene black
    with fade
    scene bg central_hub
    with dissolve_scene_full
    "We walk for fifteen minutes toward the mall."
    "I immediately see a 50th year anniversary banner right outside."
    "As we walk closer to the entrance, I see a sign by one of the entrances."
    "\"Due to technical difficulties, the 50th Anniversary Christmas Lights have been cancelled today. We apologize for any inconvenience. Sincerely, Management\" "
    show natsuki 5bq at t11 zorder 2
    n "Damn it. That really sucks."
    n 5be "You'd expect them to be ready for the holidays."
    mc "That's a first."
    mc "Well, we can always go to another one."
    mc "The train station is nearby, and there's direct service to the main city."
    n 2ba "Sure, but you gotta pay for my fare."
    mc "Eh? But it's not even that expensive."
    n 2bb "Yeah yeah, let's go, let's go."
    show natsuki at thide
    hide natsuki
    scene bg subway_afternoon
    with wipeleft_scene
    "Natsuki drags me towards the station and we jump on the first express train to the city."
    "After a few short stops, we finally get to our destination."
    scene bg station_day
    with wipeleft_scene
    "A mass of people are walking towards the mall."
    "Natsuki grabs my hand as we rush forward."
    "We reach our destination, along with fifty other people who got off our train."
    scene bg christmas_tree
    with wipeleft_scene
    show natsuki 1bk at t11 zorder 2
    n "Holy crap..."
    n "It's beautiful."
    mc "You're beautiful, Nats."
    show natsuki 4bv at d11 zorder 2
    "Natsuki proceeds to slug me in the arm."
    show natsuki at thide
    hide natsuki
    mc "Ow, what the heck!"
    "I'm about to tell Nats off, but I see her eyes glittering as she looks at the lights around us."
    "We walk around the tree for a while, basking in the lights around us."
    show natsuki 1bd at t11 zorder 2
    n "Hey, [player]?"
    mc "What's up?"
    n "Can you buy me a churro? The one with chocolate?"
    mc "It's like breakfast time, though."
    n 5bx "Get me a churro, dummy, or {i}you're{/i} not getting any dinner tonight."
    mc "Okay, okay, fine."
    show natsuki at thide
    hide natsuki
    "Natsuki sticks her tongue out at me as I walk away."
    "I wait in line for a churro for about three minutes, hoping that the family of five in front of me doesn't buy out the entire stand and leave me with nothing but a cup of cinnamon sugar."
    "I finally reach the front of the line and manage to order the last churro, eliciting a collective groan from everyone behind me."
    "I walk over to Natsuki and hand it to her."
    show natsuki 3bl at t11 zorder 2
    n "Thank you! Want a bite?"
    mc "How could I deprive you of any sweets? It's all yours."
    show natsuki 3bj
    "Natsuki smiles at me and enjoys her confection."
    show natsuki at thide
    hide natsuki
    "Walking around the tree a little more, we quickly get bored and decide to call it a day."
    scene black
    with fade
    "Natsuki and I take the next train home, and arrive back at our house at about five o'clock."
    "Looks like I'm gonna have to get used to it being dark so early in the day again."
    scene bg living_room_night
    with wipeleft_scene
    show natsuki n1bc at t11 zorder 2
    n "I'm super tired for some reason, I think I need a nap."
    mc "Really? I was just about to make dinner for us."
    n n4bb "I guess all that walking around tuckered me out."
    n "I just need a nap, but I'll make myself something when I wake up."
    mc "Well, I could make us both something and you can just warm it up later."
    n n2bh "Well, that's nice of you to offer, but...er, nothing."
    mc "What is it?"
    mc "Do you not trust me to make a good meal?"
    n n2bs "Um...I'm gonna go...take that shower now."
    n n2bt "If you're done being insecure about your lack of cooking skills, feel free to join me."
    n n1bj "Ehehehe~!"
    show natsuki at thide
    hide natsuki
    "Natsuki winks at me while walking away."
    "{i}How dare she tease me like that.{/i}"
    "I make myself a quick dinner, which I wouldn't necessarily blame Natsuki for not wanting to eat, then finish it in less than two minutes."
    "I sling my dish into the sink to worry about it later, then, hormones running through me, knock on the bathroom door."
    n "It's unlooocked~!"
    "I open the bathroom door, letting the sight fill my eyes."
    stop music fadeout 1.5
    "What an end this'll be to a great day."
    scene black
    with fade
    #End Scene 9 - Cocoa

    #Start Scene 10 - Inertia
    scene bg bedroom
    with dissolve_scene_full
    play music t12
    "Natsuki and I have been through a lot lately, I think that much is clear."
    "However I'm proud of her for the way she's carried herself as of late, and even when she has made mistakes, I've helped her in learning from them and growing more."
    "Our date to see the Christmas lights around the city was five days ago. Christmas is coming up soon, in about a week, and I'm ready to start thinking about what to get her."
    "Maybe I should've thought of that earlier but I've been...busy."
    "It's about eleven in the morning, and I'm still yet to get out of bed."
    "Natsuki woke up before me, so I figured she wanted to do some household chores."
    "Am I a freeloader in my own house?"
    "Eh, whatever."
    "I stand up and go about my morning routine, about an hour later than I normally would."
    scene bg kitchen
    with wipeleft_scene
    "I go to the kitchen expecting to see Natsuki making breakfast, but she isn't there."
    scene bg livingroom
    with wipeleft_scene
    "I walk into the living room, and that's where I do find her, but..."
    "She's crying..."
    mc "Nats?"
    show natsuki 1bp at t11 zorder 2
    n "GYAAH!"
    n 4bw "[player], don't freaking scare me like that!"
    "I decide to ignore the fact that I consciously entered the room as delicately as I could and go straight to asking her a question."
    mc "Are you okay? How come you're crying?"
    n 2bs "O-oh...well..."
    "I look more closely at Natsuki's arms and see her clutching something to her chest."
    mc "What's that?"
    n "..."
    n 1bu "Its, uh..."
    "Natsuki holds the object out to me and I see what it is."
    play music t10
    n "It's...my mom's cookbook."
    mc "Oh, Nats..."
    "I go next to her on the couch and she instinctively rests her head on my shoulder."
    mc "Are you doing alright?"
    n "I'm...okay, I guess. I just can't help but think about her today."
    n 1bn "It's her birthday tomorrow."
    mc "Is that so?"
    n 1bm "Y-yeah. Every year I open this book and find a new recipe to cook that I haven't made before, and then I go visit her at the cemetery."
    n "I sit there and eat it and I stay and talk to her for a few hours."
    n 2bq "Last year was the first time I went by myself without my dad."
    n 2bs "And it was...really relaxing."
    mc "How come he didn't go with you?"
    n 3bk "He was busy working that day. He would've, otherwise."
    n "But I'm glad he didn't. If he was there with me I wouldn't have been able to talk to my mom as much as I would've liked."
    n 3bs "Whenever I would try to in the past, he'd look at me with such reproach, I couldn't stand it."
    "I can't believe her dad."
    "It must be because of him and his constant torment over his daughter in the past that made Natsuki believe that everything that happened to their family was her fault."
    mc "So...I assume you'll be going there by yourself tomorrow as well?"
    n 4bq "Well, actually, I was going to ask you if you wanted to come with me."
    mc "A-are you sure?"
    n 5bt "Heh, why, are you scared of meeting your girlfriend's mom?"
    mc "Heh, n-no, that's not it. It's just..."
    mc "It's a really intimate and important day for you, isn't it? Are you sure you want me there?"
    mc "I just wouldn't want to ruin it for you like your dad used to."
    n 1bn "[player], come on, you could never."
    n 4bs "My dad...he's troubled, unlike you."
    n 5bh "It's important and intimate, yeah, but there's no one more important to me than you, and no one I'm more comfortable being intimate with than you."
    n 5bu "I want you there with me tomorrow."
    n 2bt "And besides, you'll be able to have an amazing lunch prepared in spirit by my mom, the only person I ever knew who was a better cook than me!"
    n 2bz "Who says no? Hehehe~!"
    mc "Well when you put it that way, I guess I can't. Haha."
    mc "But really, if it's important to you that I'm with you tomorrow, I'll be happy to join you."
    n 1bj "Thank you, [player], I love you."
    mc "By the way, could I ask you one more thing?"
    n 1bc "What is it?"
    mc "How come... you've never told me about this before? About this tradition you have on your mom's birthday?"
    n 1bn "Well...truth be told I just don't like to talk much about my mom."
    n "Don't get me wrong, I love her, but that's exactly why I don't like talking about her."
    n 2bm "The longer I do, I...break down, eventually. And I don't like you to see me like that."
    n "I'd rather not force you to worry about me so much."
    mc "Well if I wasn't worried about you I probably wouldn't be a very good boyfriend, would I?"
    n 2bj "I guess not."
    n "I dunno, I still just have trouble opening up sometimes."
    mc "We'll get there, Nats, I promise."
    n "Thank you."
    "Natsuki kisses me on the cheek then stands up."
    n 4bd "Sorry, I've kept you waiting for breakfast for too long. I'll get working on that now."
    mc "I can help you."
    show natsuki 4ba
    "Natsuki smiles at me and accepts my offer, and the two of us go make breakfast together."
    show natsuki at thide
    hide natsuki
    scene bg living_room_sunset
    with wipeleft_scene
    "The rest of the day goes by without a hitch. The two of us stay in and watch a movie together on TV, and the closer we get to night time, the more I notice Natsuki getting a little worked up."
    mc "You okay?"
    show natsuki s1bh at t11 zorder 2
    n "I'm just a little anxious about tomorrow. I don't know if I'll be able to handle it."
    mc "But you've done this a lot, haven't you?"
    n s4bi "Once a year isn't a lot, [player], it still hurts me."
    mc "I understand. But I'll be there with you. I believe in you, Nats, you're stronger than you think."
    n s4bh "Th-thank you."
    n s1bc "Um, I think I should get to sleep now. I have to wake up early to prepare our food."
    n s4bt "My mom could really cook, but man were her recipes complicated."
    mc "Okay, Nats. Sleep well, okay?"
    n s1ba "I will, good night."
    n "I love you."
    mc "I love you too."
    show natsuki at thide
    hide natsuki
    stop music fadeout 1.5
    "Natsuki disappears into the night and prepares herself for bed, meanwhile I stay in the living room and flip on some anime."
    "As I watch, I can't help but think about tomorrow."
    "I'm admittedly a little nervous as well, for a number of reasons."
    "First, I've never really been to a cemetery before. I'm not sure how one is supposed to act."
    "Second, Natsuki's bound to be emotional, and I hope I'm supportive enough to help her."
    "Third...well, I do have a third concern, but I don't imagine it'll amount to anything."
    "I guess all there is to do is to rest and mentally prepare myself for tomorrow."
    scene black
    with fade
    scene bg livingroom
    with dissolve_scene_full
    play music t12
    "Natsuki spent the whole morning cooking for today's lunch."
    "She woke up bright and early to go shopping for ingredients, and spent about four hours preparing the food."
    "I couldn't help but sneak glances of the meal throughout the morning, and she would laugh and keep telling me to back off."
    "Eventually I put the food out of my mind. After showering I go outside and sit and wait for Natsuki."
    scene bg house
    with wipeleft_scene
    "I really hope today goes well."
    "The door opens behind me and I look at Natsuki."
    "She's carrying a bag presumably filled with takeout boxes that we'll enjoy together visiting her mom, and she's wearing a backpack, I assume filled with plates and utensils."
    show natsuki 1bu at t11 zorder 2
    n "I'm...ready."
    mc "Then let's go."
    "I take Natsuki's hand and we walk toward the cemetery."
    "I hear Natsuki sniffle a few times as we walk, but it can't be helped."
    scene bg cemetery
    with dissolve_scene_full
    "Once we walk in through the gates of the cemetery, Natsuki releases my hand from her grip and starts power walking in the direction of her mother's grave."
    "I let her continue how she likes and decide I'll reach her at my own pace."
    "In the distance I see Natsuki standing a few yards in front of a headstone, and she has her hands behind her back, almost bashful."
    "Seems like she's a little kid again, showing her mom a drawing she made, but by the looks of it she just seems to be too shy to go any closer."
    "I finally make it to her and put my hand on her shoulder."
    "I look at the headstone, reading '{i}Kaiya Shimizu, 1974-2007, Mother, Wife, Friend'.{/i}"
    "There's a few wilted flowers taped to the stone, with a photo of her and Natsuki."
    "I look at the photo and as I look at Natsuki's mother it's like I'm looking at Natsuki."
    "She looked just like her."
    "{i}Ms. Shimizu, I hope you know I'm always by your daughter's side.{/i}"
    mc "Go ahead, Nats. You can talk to her."
    stop music fadeout 1.5
    "Natsuki nods her head and sniffles."
    show natsuki 1bu at t11 zorder 2
    play music t9
    n "H-hi, Mama."
    n "I know it's been a while, I'm really sorry. I wish I had the courage to come see you more."
    n "B-but I'm glad I can be here with you now."
    n 1bt "A-a-and, look, Mama, I want you to meet someone."
    "Natsuki walks back to me and grabs my hand and I walk with her."
    n "Mama, this is [player]. He's my boyfriend. I've been...living with him for a long time."
    mc "H-hi, Ms. Shimizu."
    mc "It's great to finally meet you. Natsuki's told me so much."
    "I decide to stop there. I don't want to force anything I say."
    "Natsuki then sits down on the grass by her mother's headstone and I follow suit."
    n 4bl "Mama, look, I made the pork udon, from your cookbook."
    n 4bm "I...didn't really have time to make fresh noodles, so I just bought some that was prepared for me, b-but I still think it turned out really good!"
    n "I just know that if you were here for me...you'd make it so much better than I ever could."
    n 4bn "You were always..."
    n 42bd "So good..."
    n 12bf "At cooking for me..."
    "..."
    "I scooch in closer to Natsuki and hug her."
    n 2bt "*sniffle* Well, uh, anyway, I think it's time for lunch. Right, [player]?"
    mc "Y-yeah. I'm sure your food will make your mom very proud, Natsuki."
    "I kiss her on the head."
    show natsuki at thide
    hide natsuki
    "We then head towards the picnic area, and sit down at a wooden table."
    "We're completely alone. The silence of the cemetery is eerie, but in a peaceful way."
    "Nats opens the backpack she brought with her and pulls out a tablecloth, then spreads it across."
    "I help her get it nice and even, then she pulls out two plastic plates and two pairs of chopsticks."
    "Finally she opens the plastic bag, and subsequently opens the styrofoam box containing the food."
    "It smelled good before, but now that it's out in the open, it's absolutely heavenly."
    "Natsuki puts an equal amount of noodles on each plate, then hands me mine."
    "Then, without any regard for cleanliness or properness she breaks her chopsticks apart, takes a bunch of noodles and..."
    "Drops it straight on her face, missing her mouth completely, and it lands safely back on her plate."
    "I look at Natsuki, dumbfounded with what I just saw, and she notices."
    show natsuki 4bd at t11 zorder 2
    n "Ahahaha~! I'm sorry, I promise I'm not crazy."
    n 3ba "I just forgot to tell you, that's something I always used to do with the noodles my mom made for me when I was little."
    n "It...always made her laugh."
    mc "Oh, I see. I do have to admit, it's pretty funny."
    mc "Your mom must have had a great sense of humor, I can see where you got it from."
    n 3bz "Ehehehe~! Yeah, my mom was a super funny lady. She always loved to do silly voices and make silly faces with me."
    n 2bd "She told me the corniest knock knock jokes when I was too little to understand how dumb knock knock jokes are."
    mc "Oh, I'd love to hear one."
    n 4be "Do I have to?"
    mc "It would make my day."
    n 5bi "...Fine. Um..."
    n "Okay, I remembered one."
    n 5bh "Knock knock."
    mc "Who's there."
    n 4bb "A broken pencil."
    mc "A broken pencil, who?"
    n 2be "{i}Never mind! It's pointless!{/i}"
    mc "Pft..."
    mc "Ahahaha! That was actually kinda funny."
    n 2bd "I guess you're right."
    n 1ba "Well, uh, anyway, let's eat our food before it gets too cold."
    mc "Come on, one more jo--"
    n 5bx "Nope. Shut up and eat."
    show natsuki at thide
    hide natsuki
    "We laugh and I take a whiff of the delectable smell. Natsuki's now begun to eat her noodles normally, and I do the same."
    "I take my first bite, and I'm astounded. The spices are on point, and the vegetables are steamed to perfection."
    "I take a bite out of some pork, and it's absolutely splendid."
    mc "Nats, you've outdone yourself this time."
    show natsuki 2bl at t11 zorder 2
    n "Thanks, I'm glad you think so. To be the best, I had to learn from the best."
    n "So if you're going to thank anyone, thank my Mama."
    mc "You know..."
    show natsuki 2bk
    mc "I think I'll do just that."
    show natsuki at thide
    hide natsuki
    "I stand up, then walk back over to Natsuki's mother's headstone, and Natsuki follows behind me."
    "I look at the photo of Natsuki and her mother, and look straight into her mother's eyes."
    mc "Ms. Shimizu...before I met Natsuki, I was living a pretty unremarkable life."
    mc "I'd go about the same boring drawn out daily routine, and that's what I planned to do every day for every year for the rest of my life."
    mc "But...when I met Natsuki...everything changed for me."
    mc "Natsuki's done nothing but make me smile, make me laugh, and challenge me to be a better person for as long as we've been together."
    mc "And I know that all of the roadblocks she's faced over the years, including losing you, has caused her untold amounts of distress and sadnessâ€¦"
    mc "But I feel proud to say that I've done what I could to help Natsuki channel that sadness into strength."
    mc "Natsuki is now the strongest woman I know, and if you could stand in front of her right now, I'm confident you'd see that too."
    "..."
    show natsuki 1bq at t11
    n "[player]..."
    n 12bc "[player]!!"
    "Natsuki bounds toward me and lovingly wraps her arms around me from the side."
    show natsuki 12bh at t11 zorder 2
    n "Thank you for saying all those nice things about me to my mom."
    n "It means so much..."
    n 12bi "Knowing that she knows how much I've tried to grow up without her around...!"
    n 12bf "Mama...I miss you so much..."
    stop music fadeout 2.5
    n "Being with [player] has made me realize how fleeting a relationship with anyone can be, even with your own mother."
    n 12bi "Or...with a close friend, or a boyfriend, or..."
    $ k_name = "???"
    show natsuki 1bo at t11
    k "With your own father?"
    $ k_name = "Katashi"
    mc "...!"
    n 12be "W-who's there?"
    show natsuki at thide
    hide natsuki
    "Natsuki squeezes me tightly, then closes her eyes and doesn't look in the direction where the voice came from."
    "However, I do the honors, and I tap Natsuki on the shoulder as if to say, 'Look.'"
    "She finally does, and it seems as if her and my worst fear had been realized."
    "The look on Natsuki's face is unlike any look of fear I've ever witnessed."
    "She's feeling more than just fear. She's feeling anger, sadness, resentment..."
    "{i}Hatred.{/i}"
    show katashi 1c at t11
    "..."
    k 1d "Natsuki..."
    show katashi 1c at t21
    show natsuki 42ba at f22 zorder 2
    n "P-p-p..."
    n "Dad."
    show katashi 5a at t21
    n 42bb "I...didn't think I'd see you here. I thought you'd be..."
    n "Doing..."
    n 42bc "Or that you'd be working at...er..."
    show katashi 5c at f21
    show natsuki 42bb at t22
    "Her father scoffs slightly."
    k 5d "I guess we're at the point now where neither of us have any idea what the other is even up to, huh, Natsuki?"
    show katashi 5d at t21
    show natsuki 12bb at f22
    n 12bb "Dad..."
    n 12ba "What are you doing here?"
    show natsuki 12ba at t22
    show katashi 1w at f21
    k 1w "I can't come see my wife on her birthday?"
    k 1n "I thought you'd understand better than anyone how much this day means to me."
    show katashi 1j at t21
    show natsuki 42be at f22
    n 42be "Yeah? Then why did you miss it last year?!"
    show natsuki 42bd
    play music t10
    show natsuki 1br at t22
    show katashi 4l at f21
    k 4l "What choice did I have, Natsuki?!"
    k "I'm stuck, I have to work so many long hours at so many horribly-paying jobs to provide for you!"
    k 2o "Everything I did and everything I didn't do was all for you, Natsuki!"
    show katashi 1k at t21
    show natsuki 12bc at f22
    n 12bc "That's bullcrap, and you know it, Dad!"
    n "When mom died you didn't give a damn about me!"
    n 12ba "All you ever did was look at me like I was some kind of accident, like I was the reason everything that happened, happened!"
    n 42bb "And you know, for a long time, I believed that."
    n "For so long the night Mama died had haunted my dreams, and I'd wake up hating myself, disgusted with myself."
    n "Not because I was the reason she died, but because you always made it so."
    n 12be "You did nothing but make me wish I was never born for a whole ten years, Dad."
    n 12ba "So please, keep telling me that everything you did was to give me the so-called life I deserved."
    show natsuki 12ba at t22
    "..."
    show katashi 5h at f21
    k "Natsuki..."
    k 5i "You're right, okay?"
    k 5j "I did resent you. I did blame you for your mother's death."
    k 5n "And not that you don't already know, but that was wrong, and like a man I will admit that right now."
    k 2o "I...wasn't well, then."
    show katashi 1n at t21
    show natsuki 42bd at f22
    n 42bd "..."
    n 4be "Are you trying to say that you're somehow better now?"
    show natsuki 4bg at t22
    show katashi 1o at f21
    k "Natsuki...I'm trying. For you, I'm trying."
    show natsuki 5bf at f22
    show katashi 1n at t21
    n 5bf "What changed all of a sudden? Why do you think now I'd believe that you wanted to do anything for me?"
    show natsuki 5bf at t22
    show katashi 5j at f21
    k "Do you want to know what changed? Do you really want to know?"
    show natsuki 5bw at f22
    show katashi 5j at t21
    n 5bw "Yes, Dad, {i}please{/i} tell me!"
    stop music
    show natsuki 1bm at t22
    show katashi 4u at f21
    k 4u "{i}IT WAS BECAUSE I ACTUALLY LOST YOU THIS TIME.{/i}"
    show natsuki 1bu at f22
    show katashi 1q at t21
    n 5bi "..."
    show natsuki 1bu at t22
    show katashi 1q at f21
    k "..."
    show natsuki 1bu at t22
    show katashi 1q at t21
    mc "..."
    play music t9
    show katashi 4r at f21
    k 4r "Natsuki...I can't believe the man I had become in the last decade."
    k "For ten long years I spent every day wishing that you'd be out of my life."
    k 1t "But...the minute you were actually gone was when I woke up."
    k "It's when I realized that I was living the most despicable life any man could live."
    k 1o "Truth be told, Natsuki, I had no idea where to go after you left."
    show katashi 1o at t21
    show natsuki 5bx at f22
    n 5bx "Stop saying \"I left,\" you kicked me out!"
    show natsuki 5bh at t22
    show katashi 4l at f21
    k "{i}YOU THINK I DON'T REALIZE THAT?{/i}"
    k 1u "{i}YOU THINK THAT ISN'T SOMETHING THAT'S BEEN EATING AWAY AT ME FOR THE ENTIRETY OF THE LAST THREE MONTHS?{/i}"
    k 1v "..."
    k 3o "Natsuki, I didn't know what to do with you gone."
    k "I didn't know where you went, or how I could reach you again, and even if I did, what could I say to you?"
    k 1n "So...every week since you've been gone I've...come here, to see your mother."
    show natsuki 5bm at f22
    show katashi 1w at t21
    n "..."
    n 5bq "Really?"
    show natsuki 5bn at t22
    show katashi 1b at f21
    k "Yes. Talking to her has been a reconciliation of sorts for me."
    k 2h "I'd rack my brain trying to think of all the sins I've committed unto you both."
    k 2i "The drinking, the cursing, the yelling..."
    k 1j "The hitting..."
    k 1o "Natsuki, the more I talked to your mother about the things I'd done wrong, the more I realize how much I've made your life hell for the last ten years."
    k 3f "Because of that...I've quit drinking, and I've tried getting back on my feet."
    k 3o "I've been looking for a better job so I could refurnish the house and stock our refrigerator."
    k 4r "So that...when...{i}if{/i} you ever came home, you'd have a better place to live."
    show katashi 1q at t21
    show natsuki 5bs at f22
    n 5bs "..."
    show katashi 4v at f21
    show natsuki 5bu at t22
    k "Natsuki, I know that what I've done to you is unforgivable. I can hardly forgive myself."
    k 1r "But...if you'll give me the chance to prove that I've wanted to change--"
    stop music
    show katashi 1q at t21
    show natsuki 5br at f22
    n 5br "I'm going to stop you right there, Dad."
    play music t10
    n "There's...nothing you can do or say to convince me anything you're saying is sincere."
    show katashi 1p at f21
    show natsuki 5br at t22
    k "But...Natsuki--"
    show katashi 1q at t21
    show natsuki 5bw at f22
    n 5bw "Dad, you are the slimiest, scummiest, most horrid person I've ever known in my life."
    n 1bw "If you actually think what you've done can be erased, then you're also the stupidest person I've ever known too."
    show natsuki 1bx at t22
    show katashi 4o at f21
    k "Natsuki, please, don't say things like that..."
    k "Please, not in front of your mothe--"
    show natsuki 4bp at f22
    show katashi 1s at t21
    n 4bp "{i}DON'T BRING MAMA INTO THIS, OKAY?{/i}"
    n 42bf "{i}THIS ISN'T ABOUT HER. THIS IS ABOUT ME AND YOU.{/i}"
    n 4bo "You've scarred me in so many more ways than I can describe, Dad."
    n 42be "When you hit me for the first time...nothing made me feel less human."
    n "The only reason I gave the effort to carry on with my miserable life is that [player] gave me a reason to not think it so miserable."
    n 5be "[player] is my whole world, now, Dad, not you."
    n 5bu "Not that it's ever been you with the way you've treated me."
    show katashi 1q at t21
    n 5bx "Just...leave me alone. Leave us alone..."
    n 2br "I never wanted to see you again when you kicked me out, and now that my life is back on track, I certainly never want to see you again after I leave here today."
    n 2bf "So...just tell Mama happy birthday if you need to. You can even confess your sins to her if you think it'll help."
    n "Just know that confessing doesn't actually mean erasing, Dad."
    show natsuki 2bf at t22
    show katashi 1t at f21
    k "..."
    k 1h "..."
    k 2i "I think I've said my piece."
    show natsuki at thide
    hide natsuki
    show katashi 1n at t11
    "Natsuki's father steps between us and walks up to his late wife's headstone."
    "He begins to whisper, and I can't help but overhear."
    k 1t "(Kaiya, I'm sorry for all the wrong I've done. I wish I could change the past. But all I can do is say I'm sorry again.)"
    k 3t "(Happy birthday, my darling. I hope our princess comes home someday.)"
    show natsuki 1bu at t21
    show katashi 3c at t22
    "Natsuki's father kisses the headstone, and puts his thumb on the photograph."
    show natsuki 1bh at t21
    "He then untapes the photo from the headstone, causing Natsuki to gasp, but she says nothing."
    "Finally, he reveals that the picture was folded all along."
    "He then walks toward Natsuki, getting closer to her than she seems comfortable with, and hands her the photograph."
    show katashi 3i at f22
    k 3i "Just remember...there was a time where I wasn't the monster you see me as."
    show katashi 1a at t22
    show natsuki 12bb at f21
    n "..."
    show natsuki 12bb at t21
    "As Natsuki's father steps away from Natsuki, he turns toward me for the first time, and gives me a subtle, manly nod."
    show katashi 1a at thide
    hide katashi
    "The next moment, he's gone, completely out of sight."
    "I think about it, and I didn't even get a single word in. He didn't even acknowledge me outside of that nod."
    "Is it weird for me to be bothered by that? It's hard to tell."
    show natsuki 5bh at t11 zorder 2
    n "I'm...sorry you had to see that."
    n 5bn "Do you...think...I was too hard on him?"
    mc "It can't be helped, Nats. You had a lot you needed to get off your chest."
    mc "In your shoes I probably would've done the same thing."
    n 5bs "..."
    n 5bm "Well, if you were in my shoes...would you have accepted his apology?"
    "..."
    show natsuki 5bn

    menu:
        "Would I have?"
        "Yes":
            $ karma += 2
            show natsuki 5bh at t11
            mc "Truthfully...I think I would've."
            mc "He...seems like he's really making an effort, Natsuki."
            mc "Before today I had this picture of him and his personality in my head, but after seeing him just now..."
            mc "I guess I was expecting someone a little less...human."
            mc "And I think...every human deserves forgiveness so long as they make the effort to better themselves."
        "No":
            $ karma -= 2
            show natsuki 5bq at t11
            mc "Not a chance, Natsuki."
            mc "It doesn't matter what he's saying he's done to change his ways."
            mc "Like you said, he put you through hell for way too long to deserve any benefit of the doubt."
            mc "He had his chance for ten years to make you feel welcome. I don't think he should get any more chances."
    
    n 5bs "..."
    n "I see."
    n 1bg "I think I needed to hear that from you to know for sure."
    "Natsuki looks at the picture her dad handed her, and looking closer I can see that it's a full family photo."
    "I walk next to her and get a better look at it."
    "Her dad looks like a very different and more put together man in this picture."
    n 1bq "I...put this on Mama's headstone here last year."
    n "I could've cut it and deleted any trace that he was in it the whole time too, but..."
    n 12bb "I would've felt too guilty because..."
    n "In spite of everything..."
    n "He's still my dad, and..."
    n 12bd "Deep down..."
    show natsuki at thide
    hide natsuki
    "She doesn't finish her sentence."
    "Instead, Natsuki and I return to the picnic table, sit back down, and we both eat our lunches, ready to make the rest of the afternoon about her mother."
    "She tells me more stories of the things she and her mom did when she was younger, and all the dishes she prepared for her and how she loved to learn how to cook from her."
    "She told me about the playful back and forths they would have with each other, and it helped me understand where her stand-offish nature came from."
    "She told me about the first time her mom bought her a manga and how they read it together."
    "I'm glad to know that I've let Natsuki be herself around me, and do all the things she used to do with the most important person in her life."
    "{i}Ms. Shimizu...I'll hold onto your princess and never let go...{/i}"
    "We decide that the festivities are just about done, and Natsuki walks up to her mother's headstone one last time before she leaves."
    show natsuki 1bj at t11 zorder 2
    n 12ba "Happy birthday, Mama..."
    n 12bd "I love you."
    show natsuki at thide
    hide natsuki
    "She grabs my hand and we leave the cemetery, and go back home."
    scene black
    with fade
    stop music fadeout 2.5
    "Back to {i}our{/i} home."

    jump Natsuki6
    
    #End Scene 10 - Inertia


label Natsuki51:
#Start Scene 9a - Cocoa (Good)
    scene bg bedroom_dawn
    with dissolve_scene_full
    play music t5_natsuki
    "As soon as I wake up, I feel cold around my head."
    "That feeling is periodically being interrupted by a soft, warm breathing."
    "I turn to face the girl next to me, and just lie there for a few minutes."
    "Eventually, I gently get out of bed, trying my best to not wake up Natsuki."
    "Following my success, I quietly walk down the stairs and look outside my living room window."
    scene bg living_room_dawn
    with wipeleft_scene
    "The view outside near the road is breathtaking. Snow everywhere on the ground, all fresh and fluffy."
    "I stand there for a few minutes with my hands in my pockets, simply admiring the scene unfurled in front of me."
    "Suddenly, I realize that I forgot about what I was supposed to do."
    "I walk over to the kitchen, and look around for the proper cupboard for each item I need."
    scene bg kitchen_dawn
    with wipeleft_scene
    "Once finding the proper cupboards, I grab two coffee mugs, hot chocolate powder, a bag of marshmallows, spoons, and some muffins from the fridge."
    "I set up the cups on a plate each, and put a muffin next to each of them."
    "I pull down my nearby kettle from my cupboard, and plug it into the power socket so it can start heating up the milk for the hot chocolate."
    "I turn to the coffee mugs, and measure in a few tablespoons of hot chocolate powder into each of them."
    "When the milk is done heating up, I pour a generous amount into each of the mugs, and stir."
    "As soon as I am done, I walk upstairs quietly to my sleeping beauty upstairs, hoping to surprise her with what I made for our breakfast."
    "Opening the door a bit more, I see that she is already awake and drawing cute shapes on our bedside window."
    scene bg bedroom_dawn
    with wipeleft_scene
    n "Good morning, [player]~"
    mc "Hey Nats, good morning. I made something for us."
    mc "I was planning on just staying snuggled in the blankets all morning while we enjoy it."
    n "Too late for that, but thanks anyway. What did you bring me?"
    mc "Turn around and see for yourself."
    show natsuki d1bk at t11 zorder 2
    "Turning around, I see her eyes go from a groggy glassy look to lit up like a carnival when she sees what I brought her for breakfast."
    n d1bz "Awesome!"
    n "That's so sweet of you!"
    n d1bt "You really didn't have to..."
    mc "Hey it wasn't any trouble, trust me."
    "We sit down on our bed and enjoy our muffin and hot chocolate combo."
    show natsuki at thide
    hide natsuki
    scene black
    with fade
    pause(1)
    scene bedroom
    with fade
    show natsuki blab at t11
    n "Hey [player]?"
    mc "Yeah Nats?"
    n "Could we, you know, go around town for a walk today?"
    n blac "You know, see the snow and maybe some of the decorations?"
    n blan "My family and I used to do that befo--"
    "I cut her off, almost reflexively."
    mc "I would love to take you around the city, Nats."
    n blal "Really?"
    mc "Absolutely."
    "I get up to clear our dishes, but Natsuki grabs my hand and pulls it to her face."
    "She brings my hand to her face, and leans on it."
    n blaj "Your hand is warm, [player]. Really warm..."
    "A few moments pass."
    show natsuki blaj at thide
    hide natsuki
    "She then backs away and goes to her drawers while hiding her deep red cheeks underneath her hands."
    "I walk up behind her, and go to my closet to get changed, while Natsuki takes her clothes to the bathroom."
    "After I get changed, I go downstairs and wait for Natsuki."
    scene bg livingroom
    with wipeleft_scene
    "To my surprise, she's already downstairs standing by the door with her arms crossed and a smug look on her face."
    show natsuki 5bd at t11
    n "Wipe that look off your face, [player]."
    mc "What look?"
    "I say as I turn towards the mirror."
    n 5bl "Hehehehe~!"
    n 5ba "Gotcha, idiot."
    "My reaction causes her to showcase a heavenly smile."
    mc "Anyways..."
    mc "You ready?"
    n "Yeah, I'm ready."
    show natsuki at thide
    hide natsuki
    "Natsuki and I walk out the door."
    scene black
    with fade

    return
#End Scene 9a - Cocoa (Good)

label Natsuki52:
#Start scene 9b - Cocoa (Bad)
    scene bg bedroom_dawn
    with dissolve_scene_full
    "I wake up again today, unenthusiastically."
    "It's been a harrowing few weeks for us since the events that took place in the literature club."
    "Each of us feels overwhelmingly guilty, wishing that we had gone about things differently."
    "That said, Natsuki still chooses to sleep beside me every night."
    "I feel guilty about that too, because it seems like I have her wrapped around my finger in spite of the fact that I've done nothing to try controlling her."
    "It makes me feel like a bad person. Like I'm taking advantage of my girlfriend, who I treasure more than anything."
    "Normally I'd wake up before Natsuki and just watch her sleep until she woke up, to which she would promptly respond by calling me creepy, but we have a good laugh about it."
    "Nowadays, though, all I find myself doing is opening my eyes, thinking about how much I hate myself, then go wash up and start cooking breakfast."
    scene bg kitchen_morning
    with wipeleft_scene
    "This morning I do exactly that. I perform my morning routine, then go downstairs to cook the two of us a markedly non-spectacular breakfast of eggs, rice, and toast."
    "As I'm preparing our food, Natsuki walks in through the door."
    show natsuki m1bc at t11 zorder 2
    play music t2
    n "Good morning."
    mc "Hey, good morning. How'd you sleep?"
    n m2bc "Pretty much the same as always."
    mc "Heh, yeah, makes sense."
    n "Yeah."
    "..."
    show natsuki m1bs
    "The silence is so uncomfortable."
    "I miss when we used to have little verbal back-and-forths with each other and make each other laugh."
    "Nowadays I feel like all we do is small talk like an old married couple."
    mc "U-um, hey, do you want coffee?"
    n m4bc "Yeah that sounds great! But I can make it myself, you just focus on the breakfast."
    mc "O-okay, no problem."
    show natsuki at thide
    hide natsuki
    "Natsuki opens the cupboard and starts preparing the coffee."
    "I can't help but watch her as she does so."
    "I'm just searching deep in my heart to find that spark we once had."
    "{i}Please Natsuki, give me something.{/i}"
    "{i}Do you want me to apologize? Do you want me to get on my knees and beg?{/i}"
    "{i}I hate living this way.{/i}"
    "In a way I think this is worse than her just breaking up with me."
    "..."
    n "[player]?"
    n "Are you okay?"
    mc "Huh? Why do you ask?"
    show natsuki m4bk at t11 zorder 2
    n "Because I think you're getting some of your tears in our eggs."
    n m4bb "I get if you wanna add a little extra salt for flavor but there's a line that needs to be drawn."
    mc "..."
    mc "I like to go the all naturale approach."
    n m3be "It's disgusting and you should be ashamed."
    mc "Ahahaha!"
    n m4bz "Ahahaha~!"
    "I think we're making progress."
    "The smile on my face is as plain as day."
    mc "*Ahem* Sorry, Nats."
    mc "I was just thinking about something, but I think you're helping me take my mind off it."
    stop music fadeout 1.5
    n m1bc "Oh, really? What were you thinking about?"
    mc "..."
    play music t9
    mc "Nats...do you think it's possible to miss someone dearly despite the fact that you're with them every single day?"
    mc "You miss how things were before, and wish that the spark would come back?"
    mc "You retrace your steps to figure out exactly what went wrong and in what order?"
    mc "Hoping that magically things go back to the way they used to be?"
    n m1bn "..."
    n m1bm "I...think it is possible, [player]."
    n "We...haven't been doing so well lately, huh?"
    mc "Not really, no."
    mc "I miss you so much, Natsuki."
    mc "I don't want us to keep drifting apart like this."
    "It seems like those eggs are gonna keep getting seasoned after all..."
    n m12bh "H-hey, [player], don't start crying, because if you cry then..."
    n "I'm gonna..."
    n m12bi "[player]!"
    "Natsuki leaps into my arms and I hold her tight."
    mc "I'm so sorry I let things get this way, Nats."
    n m12bf "No-o-o-o, I'm sorry... it was all my fault again..."
    mc "Nats..."
    "Natsuki and I sob into each other's shoulders."
    "I don't know if this is really what needed to happen to get the two of us back on track, but..."
    "It's something."
    mc "Natsuki, I need to make things up to you somehow."
    mc "Why don't we go on a date today?"
    n m12ba "I'd...really like that."
    n m1bs "Um, there actually is something I've been wanting to go do lately."
    mc "What would that be?"
    n "Christmas is coming soon, so...can we go to the city and see all the lights?"
    mc "Natsuki, that sounds like an amazing time."
    n m4bt "Of course it is. I came up with it, after all."
    mc "Oh, you came up with Christmas lights? Then I think the entire world ought to be thanking you for your contributions to the holidays."
    n m3bl "Pffft, don't be a dummy, dummy."
    mc "Ehehehe..."
    show natsuki at thide
    hide natsuki
    "Instinctually I hug Natsuki again and squeeze her, then we close it with a kiss."
    "{i}It really feels like it's been forever since either of us have felt this way.{/i}"
    mc "We can go as soon as we finish breakfast."
    show natsuki m1bd at t11 zorder 2
    stop music fadeout 1.5
    n "That sounds like a plan to me~!"
    show natsuki at thide
    hide natsuki
    scene black
    with fade
    play music t5_natsuki
    
    return
#End Scene 9b - Cocoa (Bad)
